/*
 * This idl file is generated by the ORB Plugin
 */
module repositorio {
	// Forward reference para poder usar IRepositorio antes de declararla
	interface IRepositorio;
	interface ITransferencia;
	
	// Lista de nombres de los repositorios subordinados
	typedef sequence<string> StringSeq;
	typedef sequence<IRepositorio> IRepositorioSeq;
	
	struct ArchivoDetalles {
		string nombre;
		string archivo;
		string directorio;
		StringSeq palabrasClave;
		string comentario;
	};
	
	struct Coincidencia {
		string nombreRepositorio;
		IRepositorio repositorio;
		string nombre;
		string archivo;
		StringSeq palabrasClave;
		string comentario;
	};
	
	typedef sequence<Coincidencia> Resultados;
	typedef sequence<octet> Bytes;

	exception ArchivoNoEncontradoException {
		string message;
	};

	exception RegistroNoPosibleException {
	};

	interface IRepositorio {
		///////////////////////////////////
		// Constantes
		///////////////////////////////////
		const string CLAVE = "myself.repo";
		const string CLASE = "repositorio.IRepositorio";

		///////////////////////////////////
		// Atributos
		///////////////////////////////////
		attribute IRepositorio padre;
		readonly attribute IRepositorioSeq subordinados;
		attribute string nombre;
		
		///////////////////////////////////
		// Operaciones
		///////////////////////////////////
		
		// Búsqueda por palabras clave
		Resultados buscar(in string palabraClave);
		
		// TODO Recuperar documentos
		ITransferencia iniciarDescarga(in string nombre) raises (ArchivoNoEncontradoException);

		
		// Registrar repositorios subordinados
		string registrar(in IRepositorio referencia) raises (RegistroNoPosibleException);
		string registrarConNombre(in IRepositorio referencia, in string nombre) raises (RegistroNoPosibleException);

		// Dar de baja un repositorio
		void baja(in string nombre);
	};
	
	///////////////////////////////////
	// Transferencias de archivos
	///////////////////////////////////
	
	interface ITransferencia {
		Bytes solicitarBloque() raises (ArchivoNoEncontradoException);
	};
};
